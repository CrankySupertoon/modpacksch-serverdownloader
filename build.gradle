buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
    }
}


plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "com.install4j.gradle" version "8.0.4"
    id "com.palantir.git-version" version "0.12.2"
}

apply plugin: "maven"
apply plugin: "java"
apply plugin: "eclipse"

repositories {
    mavenCentral()
    gradlePluginPortal()
}


group = "net.creeperhost"
archivesBaseName = "modpackserverdownloader"
ext.details = versionDetails()
version = details.lastTag;
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "11"

ext {
	MAIN_CLASS = "net.creeperhost.ftbserverdownloader.Main"
    MANIFEST = manifest{
		attributes('Main-Class': MAIN_CLASS)
        attributes([
                "Implementation-Title":     "ModpackServerDownloader",
                "Implementation-Version":   project.version,
                "Implementation-Vendor":    "Creeperhost LTD"
        ] as LinkedHashMap, "net/creeperhost/ftbserverdownloader/")
    }
}

dependencies {
    compile group: "com.google.code.gson", name: "gson", version: "2.8.5"
    compile group: "com.squareup.okhttp3", name: "okhttp", version: "4.3.0"
    compile group: "org.apache.commons", name: "commons-compress", version: "1.2"
}


import org.apache.tools.ant.filters.ReplaceTokens

task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.property 'version', version
    filter(ReplaceTokens, tokens: [VERSION: version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
    options.encoding = "UTF-8"
    dependsOn processSource
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
    manifest.from(MANIFEST)
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

jar {
    manifest.from(MANIFEST)
}

wrapper {
    gradleVersion = "6.0"
}

